#
# File locations
#
if [ -z ${DOT_FILES+x} ]; then
  export DOT_FILES=$HOME/.dotfiles
fi
if [ -z ${PROJECTS+x} ]; then
  export PROJECTS=$HOME/Code
fi

# Source local settings excluded from dotfiles repo
if [[ -a $HOME/.localrc.before ]]
then
	source $HOME/.localrc.before
fi

# Source ZSH files from dotfiles
typeset -U config_files
config_files=($DOT_FILES/**/*.zsh)

# Load everything, excluding path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit -u

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# Source local settings excluded from dotfiles repo
if [[ -a $HOME/.localrc ]]
then
  source $HOME/.localrc
fi

unset config_files
